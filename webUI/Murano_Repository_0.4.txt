*** Settings ***
Suite Setup
Suite Teardown    Close All Browsers
Test Setup        Open Browser    ${url}    remote_url=http://172.18.124.233:4444/wd/hub    browser=firefox    ff_profile_dir=ffprofile
Test Teardown     Close All Browsers
Library           String
Library           Boffin.WebUIlib    0    20    # Boffin framework library
Resource          Resources/objrepo/keywords.txt

*** Variables ***
${resources_path}    Resources/
${url}            http://10.100.0.6/horizon
${files_path}     /Files_for_autotests/

*** Test Cases ***
Create test environment
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Create Environment"
    User set value "test" for field Environment Name
    User click on "Create"
    Reload Page
    Page should contain element "test"

Check opportunity to compose a new service in repository
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    User click on "Compose Service"
    User set value "New Test Service" for field Service_Name
    User set value "newService" for field Fully Qualified Service Name
    User set value "<strong> New Test Service </strong> Here is a new description" for field Description
    User click on "UI Files"
    Choose Demo.yaml (set checkbox on in the 3 column)
    User click on "Workflows"
    Choose Demo.xml (set checkbox on in the 3 column)
    User click on "Heat Templates"
    Choose Demo.template (set checkbox on in the 3 column)
    User click on "Scripts"
    Choose CopyPrerequisites.ps1 (set checkbox on in the 3 column)
    User click on "Submit"
    Page should contain element "New Test Service"

Check opportunity to toggle service (make it active/inactive)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Select "More" for chosen service "Internet Information Services"
    Select "Toggle Active" for chosen service "Internet Information Services"
    Check that Active of element "Internet Information Services" is "False"
    Select "More" for chosen service "Internet Information Services"
    Select "Toggle Active" for chosen service "Internet Information Services"
    Check that Active of element "Internet Information Services" is "True"

Modify Service(change name)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Select "Modify Service" for chosen service "Linux Apache"
    User set value "Modified Linux Apache" for field Service_Name
    User click on "Submit"
    Check that Active of element "Modified Linux Apache" is "True"
    User click on "Environments"
    User click on "test"
    User click on "Create Service"
    Page should contain element "Modified Linux Apache"

Modify Service (change version)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Select "Modify Service" for chosen service "Linux Telnet"
    User set value "Linux Telnet" for field Service_Name
    User set value "version1" for field Version
    User click on "Submit"
    Select "More" for chosen service "Linux Telnet"
    Select "Manage Service" for chosen service "Linux Telnet"
    Page should contain element "Version"
    Page should contain element "version1"

Modify Service (change description)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Select "Modify Service" for chosen service "Active Directory"
    User set value "1.0" for field Version
    User set value "<strong> Active Directory </strong> Here is a new description" for field Description
    User click on "Submit"
    Select "More" for chosen service "Active Directory"
    Select "Manage Service" for chosen service "Active Directory"
    Page should contain element "Description"
    Page should contain element "<strong> Active Directory </strong> Here is a new description"

Download Service
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    User click on "Upload Service"
    User click on "Browse"

Check opportunity to select composed service in Service Type list
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Compose a new service "Test service"
    Navigate To    Murano>Environments
    User click on "test"
    User click on "Create Service"
    User select "Test service" from dropdown list "Service Type"
    User click on "Next"
    Page should contain element "Service Name"

Check opportunity to delete service from repository
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Compose a new service "Service for deletion"
    Select "More" for chosen service "Service for deletion"
    Select "Delete Service" for chosen service "Service for deletion"
    Page should not contain element "Service for deletion"

Check opportunity to toggle files (make few services active/inactive)
    [Tags]    night
    Log in WebUI by WebTestUser/swordfish
    User click on "Service Definitions"
    Choose Internet Information Services (set checkbox on in the 1 column)
    Choose Active Directory (set checkbox on in the 1 column)
    User click on "Toggle Active"
    Check that Active of element "Internet Information Services" is "False"
    Check that Active of element "Active Directory" is "False"
    Choose Internet Information Services (set checkbox on in the 1 column)
    Choose Active Directory (set checkbox on in the 1 column)
    User click on "Toggle Active"
    Check that Active of element "Internet Information Services" is "True"
    Check that Active of element "Active Directory" is "True"


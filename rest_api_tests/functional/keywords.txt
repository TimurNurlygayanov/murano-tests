*** Settings ***
Library           simple_REST.py
Library           Collections

*** Variables ***
${ip}             172.18.79.80
${ip_keystone}    172.18.124.201
${user}           admin
${project}        admin
${password}       swordfish
${template}       jenkinss
${url}            http://?
${method}         GET

*** Keywords ***
User send a POST request to '${end_point}' with body ${body}
    ${request_body}    Set Variable    ${body}
    Set Global Variable    ${url}    http://${ip}:8082${end_point}
    Set Global Variable    ${method}    POST
    Log    "POST request on link ${url} with body ${request_body}"
    Set Body    ${request_body}
    POST Request    ${url}

User send a POST request to '${end_point}' without body
    Set Global Variable    ${url}    http://${ip}:8082${end_point}
    Set Global Variable    ${method}    POST
    Log    "POST request on link ${url} without body"
    POST Request Without Body    ${url}

User send a PUT request to '${end_point}' with body ${body}
    ${request_body}    Set Variable    ${body}
    Set Global Variable    ${url}    http://${ip}:8082${end_point}
    Set Global Variable    ${method}    PUT
    Log    "PUT request on link ${url} with body ${request_body}"
    Set Body    ${request_body}
    PUT Request    ${url}

User send a GET request to '${end_point}'
    Set Global Variable    ${url}    http://${ip}:8082${end_point}
    Set Global Variable    ${method}    GET
    Log    "GET request on link ${url}"
    GET Request    ${url}

User send a DELETE request to '${end_point}'
    Set Global Variable    ${url}    http://${ip}:8082${end_point}
    Set Global Variable    ${method}    DELETE
    Log    "DELETE request on link ${url}"
    DELETE Request    ${url}

Get Environment ID by name
    [Arguments]    ${name}
    ${id}    Set Variable    None
    User send a GET request to '/environments'
    The response code should be 200
    ${body}    Get Response Body
    @{environments}    Convert To List    ${body['environments']}
    : FOR    ${x}    IN    @{environments}
    \    ${id} =    Set Variable If    "${x['name']}" == "${name}"    ${x['id']}
    [Return]    ${id}

The response code should be ${status_code}
    ${response_body}    Get Response Body
    ${response_code}    Get Response Code
    Run Keyword If    ${status_code} != ${response_code}    Fail    "User has sent ${method} request with url ${url}. Response code: ${response_code}. Response body: ${response_body}"

The response body should be ${body}
    ${response_body}    Get Response Body
    Log    "Response body: ${response_body}. Expected body: ${body}"
    Should Be Equal    ${body}    ${response_body}

The response body should have keys
    [Arguments]    @{keys}
    ${body}    Get Response Body
    : FOR    ${key}    IN    @{keys}
    \    Log    "Expect ${key} in body ${body}"
    \    Log    ${body['${key}']}

The response body should have key '${key}' with list ${value}
    ${body}    Get Response Body
    ${array}    Convert To List    ${value}
    Log    "Expect ${key} == ${array} in body ${body}"
    Should Be Equal    ${body['${key}']}    ${array}

The response body should have key '${key}' with not empty list
    ${body}    Get Response Body
    Log    "Expect list ${key} in body ${body}"
    ${length}    Get Length    ${body['${key}']}
    Should Not Be Equal As Integers    ${length}    0

The response body should have key '${key}' with value "${value}"
    ${body}    Get Response Body
    Log    "Expect ${key} == ${value} in body ${body}"
    Should Be Equal    ${body['${key}']}    ${value}

The response body not should have key '${key}' with value "${value}"
    ${body}    Get Response Body
    Log    "NoExpect ${key} == ${value} in body ${body}"
    Should Be Not Equal    ${body['${key}']}    ${value}

The response body parameter '${para}' should have key '${key}' with value "${value}"
    ${body}    Get Response Body
    Log    "Expect ${para}.${key} == ${value} in body ${body}"
    Should Be Equal    ${body['${para}']['${key}']}    ${value}

Set Default Headers
    Set Headers    {"Content-Type": "application/json"}
    ${body}    Set Variable    {"auth": {"tenantName": "${project}", "passwordCredentials": {"username": "${user}", "password": "${password}"}}}
    Set Body    ${body}
    POST Request    http://${ip_keystone}:5000/v2.0/tokens
    The response code should be 200
    ${body}    Get Response Body
    Update Headers    X-Auth-Token    ${body['access']['token']['id']}
